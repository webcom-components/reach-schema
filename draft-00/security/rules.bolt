/**************************************
* Global definitions
**************************************/

type Extra extends Object {
    extra: Null | Any
}

/**************************************
* Users
**************************************/

type User extends Extra {
    name: Null | String
    status: Null | String
    lastSeen: Null | Number
    provider: Null | String
}

type Device {
    status: DeviceStatus
    sdk: DeviceSdk
    os: Null | String
    userAgent: Null | String
}

type DeviceSdk {
	reach: String
	webcom: String
}

type DeviceStatus extends String {
    validate() { this == 'CONNECTED' || this == 'DISCONNECTED' || this == 'NOT_CONNECTED' || this == 'SLEEPING' }
}

// Only authenticated users can read the Users list
path /users {
    read() { auth != null }
}

// Only the user can modify its data
path /users/{userId} is User {
    write() { auth != null && auth.uid == userId }
}

// Only the user can see/modify its devices
path /_/devices/{userId} is Device[] {
    read() { auth != null && auth.uid == userId }
    write() { auth != null && auth.uid == userId }
}

/**************************************
* Rooms
**************************************/

type Room extends Extra {
    name: Null | String
    owner: Null | String
    status: Null | String
	_public: Null | Boolean
    _created: Null | Number
    _closed: Null | Number
}

type RoomMeta extends Extra {
    name: Null | String
    owner: Null | String
	_public: Null | Boolean
}

// Only authenticated users can read the Rooms list
path /rooms {
    read() { auth != null }
}
// Only owner can modify the room
path /rooms/{roomId} is Room {
	create() { auth != null && this.owner == auth.uid }
	update() { auth != null && prior(this).owner == auth.uid }
	delete() { auth != null && prior(this).owner == auth.uid }
}
// Only participants can read the private room data
path /_/rooms/{roomId} {
    read() { auth != null && (this.meta._public == true || this.participants[auth.uid] != null) }
}
// Only the owner can write the room meta
path /_/rooms/{roomId}/meta is RoomMeta {
	create() { auth != null && this.owner == auth.uid }
	update() { auth != null && prior(this).owner == auth.uid }
	delete() { auth != null && prior(this).owner == auth.uid }
}

/**************************************
* Participants
**************************************/

type Participant extends Extra {
    status: Null | String
    role: Null | Role
    _joined: Null | Number
}

type Role extends String {
    validate() { this == 'OWNER' || this == 'MODERATOR' || this == 'NONE' }
}

// The Owner can write in the participants list of his room
// The Moderator can write anywhere in the participants list
// The participant can only write his data but not elevate his role
path /_/rooms/{roomId}/participants/{userId} is Participant {
	create() {
		auth != null &&
		(
			prior(this).parent().parent().meta._public && userId == auth.uid ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent()[auth.uid] != null && prior(this).parent()[auth.uid].role == 'MODERATOR'
		)
	}
	update() {
		auth != null &&
		(
			userId == auth.uid && (this.role == null || this.role == prior(this).role) ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent()[auth.uid] != null && prior(this).parent()[auth.uid].role == 'MODERATOR'
		)
	}
	delete() {
		auth != null &&
		(
			auth.uid == userId ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent()[auth.uid] != null && prior(this).parent()[auth.uid].role == 'MODERATOR'
		)
	}
}

/**************************************
* Messages
**************************************/

type Message {
    from: String
    text: String
    _created: Number
    _end: Null | Number
}

path /_/rooms/{roomId}/messages/{msgId} is Message {
	create() {
		auth != null &&
		this.from == auth.uid &&
		prior(this).parent().parent().participants[auth.uid] != null
	}
	update() {
		auth != null &&
		(
			prior(this).from == auth.uid ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent().parent().participants[auth.uid] != null && prior(this).parent().parent().participants[auth.uid].role == 'MODERATOR'
		)
	}
	delete() {
		auth != null &&
		(
			prior(this).from == auth.uid ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent().parent().participants[auth.uid] != null && prior(this).parent().parent().participants[auth.uid].role == 'MODERATOR'
		)
	}
}

/**************************************
* Streams
**************************************/

type StreamData {
    from: Null | String
    device: Null | String
    type: Null | StreamType
    muted: Null | StreamMute
    height: Null | Number
    width: Null | Number
}
type StreamMute {
	audio: Boolean
	video: Boolean
}
type StreamType extends String {
    validate() { this == 'audio' || this == 'video' || this == 'audio-video' || this == 'chat' }
}

path /_/rooms/{roomId}/streams/{streamId} is StreamData {
	create() { auth != null && this.from == auth.uid && prior(this).parent().parent().participants[auth.uid] != null }
	update() {
		auth != null &&
		(
			prior(this).from == auth.uid ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent().parent().participants[auth.uid] != null && prior(this).parent().parent().participants[auth.uid].role == 'MODERATOR'
		)
	}
	delete() {
		auth != null &&
		(
			prior(this).from == auth.uid ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent().parent().participants[auth.uid] != null && prior(this).parent().parent().participants[auth.uid].role == 'MODERATOR'
		)
	}
}

type Subscriber {
    to: Null | String
    _created: Null | Number
}

path /_/rooms/{roomId}/subscribers/{streamId} {
	delete() {
		auth != null && (
			prior(this).parent().parent().streams[streamId] != null && prior(this).parent().parent().streams[streamId].from == auth.uid ||
			prior(this).parent().parent().meta.owner == auth.uid ||
			prior(this).parent().parent().participants[auth.uid] != null && prior(this).parent().parent().participants[auth.uid].role == 'MODERATOR'
		)
	}
}

path /_/rooms/{roomId}/subscribers/{streamId}/{deviceId} is Subscriber {
	create() { auth != null && this.to == auth.uid }
	update() { auth != null && prior(this).to == auth.uid }
	delete() { auth != null && prior(this).to == auth.uid }
}

/**************************************
* Invitations
**************************************/

type Invitation {
    from: Null | String
    room: Null | String
    status: Null | InvitationStatus
    topic: Null | String
    _created: Null | Number
    _end: Null | Number
}

type InvitationStatus extends String {
    validate() { this == 'ONGOING' || this == 'ACCEPTED' || this == 'REJECTED' || this == 'CANCELED' }
}

/*path /_/invites {

}*/

path /_/invites/{userId} {
	read() { auth != null && auth.uid == userId }
}

path /_/invites/{userId}/{invitationId} is Invitation {
	read() { auth != null && (userId == auth.uid || this.from == auth.uid) }
	create() { auth != null && this.from == auth.uid }
	update() { auth != null && (userId == auth.uid || prior(this).from == auth.uid) }
	delete() { auth != null && userId == auth.uid }
}


/**************************************
* WebRTC
**************************************/

type PeerConnection {
    sdp: Null | SDP
    ice: ICECandidate[]
}

type SDP {
	type: String,
	sdp: String
}

type ICECandidate {
    candidate: String
    sdpMid: String
    sdpMLineIndex: Number
}

path /_/webrtc/{deviceIds} {
    read() { auth != null && this[auth.uid] != null }
	create() { auth != null && this[auth.uid] != null }
	update() { auth != null && prior(this)[auth.uid] != null }
	delete() { auth != null && prior(this)[auth.uid] != null }
}

path /_/webrtc/{deviceIds}/{streamId}/{deviceId} is PeerConnection;

/**************************************
* ICE servers
**************************************/

type ICEServer {
    urls: String | String[]
    user: Null | String
    pwd: Null | String
}

// Everybody can see the ice servers
path /_/ice is ICEServer[] {
    read() { true }
}
